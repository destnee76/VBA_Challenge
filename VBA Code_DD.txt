Sub MultipleYearStockData()
   
    Dim ws As Worksheet
    For Each ws In Worksheets
        Dim last_data_row_num As Long
        last_data_row_num = ws.Cells(Rows.Count, 1).End(xlUp).Row
        
        ' Initialize summary table headers
        ws.Cells(1, 9).Value = "Ticker"
        ws.Cells(1, 10).Value = "Yearly Change"
        ws.Cells(1, 11).Value = "Percent Change"
        ws.Cells(1, 12).Value = "Total Stock Volume"
        ws.Cells(2, 15) = "Greatest % Increase Value"
        ws.Cells(3, 15) = "Greatest % Decrease Value"
        ws.Cells(4, 15) = "Greatest Total Volume"
        ws.Cells(1, 16) = "Ticker"
        ws.Cells(1, 17) = "Value"
        
        Dim SummaryTableRow As Long
        SummaryTableRow = 2
        
        Dim open_price As Double
        open_price = ws.Cells(2, 3).Value
        
        Dim TTL_Stock_Vol As Double
        
        Dim i As Long
        
        ws.Range("K:K").NumberFormat = "0.00%"
        
        For i = 2 To last_data_row_num
            
            Dim Ticker_Desc As String
            Ticker_Desc = ws.Cells(i, 1).Value
            
            ' Check if the ticker name changes
            
            If ws.Cells(i + 1, 1).Value <> Ticker_Desc Then
            
                ' Accumulate total stock volume
                TTL_Stock_Vol = TTL_Stock_Vol + ws.Cells(i, 7).Value
                
                ' Calculate the close price
                Dim close_price As Double
                close_price = ws.Cells(i, 6).Value
                
                ' Calculate Yearly Change - Col "J"
                Dim yearly_Change As Double
                yearly_Change = close_price - open_price
                
                ' Calculate Percent Change - Col "K"
                Dim yearly_change_frac As Double
                yearly_change_frac = yearly_Change / open_price
                
                ' Insert data in Summary Table
                ws.Cells(SummaryTableRow, 9).Value = Ticker_Desc
                ws.Cells(SummaryTableRow, 10).Value = yearly_Change
                ws.Cells(SummaryTableRow, 11).Value = yearly_change_frac
                ws.Cells(SummaryTableRow, 12).Value = TTL_Stock_Vol
                
                ' Conditional formatting for Yearly Change
                If yearly_Change >= 0 Then
                    ws.Cells(SummaryTableRow, 10).Interior.ColorIndex = 4 ' Green
                Else
                    ws.Cells(SummaryTableRow, 10).Interior.ColorIndex = 3 ' Red
                End If
                
                ' Reset open_price for the next ticker
                open_price = ws.Cells(i + 1, 3).Value
                
                ' Reset total stock volume
                TTL_Stock_Vol = 0
                
                ' Increment the summary table row
                SummaryTableRow = SummaryTableRow + 1
            
            Else
            
            ' Accumulate total stock volume
            TTL_Stock_Vol = TTL_Stock_Vol + ws.Cells(i, 7).Value
            
            End If
        
        Next i
        
        ' Calculate and format the greatest % increase, decrease, and total volume
        Dim max_percent_increase As Double
        max_percent_increase = WorksheetFunction.Max(ws.Range("K2:K" & last_data_row_num))
        
        Dim max_percent_decrease As Double
        max_percent_decrease = WorksheetFunction.Min(ws.Range("K2:K" & last_data_row_num))
        
        Dim max_total_volume As Double
        max_total_volume = WorksheetFunction.Max(ws.Range("L2:L" & last_data_row_num))
        
        ws.Cells(2, 17).Value = max_percent_increase
            ws.Cells(2, 17).NumberFormat = "0.00%"
        
        ws.Cells(3, 17).Value = max_percent_decrease
           ws.Cells(3, 17).NumberFormat = "0.00%"
        
        ws.Cells(4, 17).Value = max_total_volume
        
        Dim ticker_max_increase As String
        ticker_max_increase = ws.Cells(WorksheetFunction.Match(max_percent_increase, ws.Range("K2:K" & last_data_row_num), 0) + 1, 9).Value
        
        Dim ticker_max_decrease As String
        ticker_max_decrease = ws.Cells(WorksheetFunction.Match(max_percent_decrease, ws.Range("K2:K" & last_data_row_num), 0) + 1, 9).Value

        Dim ticker_max_volume As String
        ticker_max_volume = ws.Cells(WorksheetFunction.Match(max_total_volume, ws.Range("L2:L" & last_data_row_num), 0) + 1, 9).Value
        
        ws.Cells(2, 16).Value = ticker_max_increase
        
        ws.Cells(3, 16).Value = ticker_max_decrease
        
        ws.Cells(4, 16).Value = ticker_max_volume
    
    Next ws
    
    MsgBox "Done"

End Sub